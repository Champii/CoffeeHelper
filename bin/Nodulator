#!/usr/bin/env coffee

#
# Nodulator Boostrap
#
# Usage: Nodulator (init) | ((install | remove) moduleName)
#

_ = require 'underscore'
fs = require 'fs'
path = require 'path'
exec = require('child_process').exec
async = require 'async'

args = process.argv[2..]

NodulatorRoot = path.resolve __dirname, '..'
appRoot = path.resolve '.'

moduleOrder = ['nodulator-socket', 'nodulator-assets', 'nodulator-angular', 'nodulator-account']

dependencies =
  'nodulator-angular': ['nodulator-socket', 'nodulator-assets']

exists = (path, done) ->
  fs.open path, 'r', (err, fd) ->
    if err?
      done false
    else
      fs.closeSync fd
      done true

npm = (action, pack, done) ->
  exec 'npm ' + action + ' ' + pack + ' --save', (err, stdout, stderr) ->
    return done err if err? and done?
    return console.error err if err?

    done() if done?

usage = ->
  console.error 'Usage: Nodulator (init) | ((install | remove) moduleName)'

generateMain = (modules) ->
  capitalize = (str) ->
    str[0].toUpperCase() + str[1..]

  mainBefore = 'Nodulator = require \'nodulator\'\n'
  mainBefore += 'Server = require \'./server\'\n'

  mainAfter = ''
  mainAfter += '\n\n' if modules.length
  for module in (_(modules).sortBy (item) -> _(moduleOrder).indexOf item)
    name = module.split('-')[1]
    mainBefore += '\n' + capitalize(name) + ' = require \'' + module + '\''
    mainAfter += 'Nodulator.Use ' + capitalize(name) + '\n'

  mainAfter += '\nServer.Init()\n'
  mainAfter += 'Nodulator.Run()\n' if 'nodulator-assets' in modules

  fs.writeFileSync appRoot + '/main.coffee', mainBefore + mainAfter

if args.length < 1 or args.length > 2 or args[0] not in ['init', 'install', 'remove']
  return usage()

if args[0] is 'init'
  async.series [
    (done) ->
      process.stdout.write 'Installing base folder tree.......'
      exec 'mkdir -p node_modules', (err, stdout, stderr) ->
        exec 'cp -ran ' + (path.resolve NodulatorRoot, 'bin/baseFiles/') + '/* ' + appRoot, done

    (done) ->
      process.stdout.write 'Ok\n'
      process.stdout.write 'Processing submodules.............'
      fs.readdir 'node_modules', (err, files) ->
        return done err if err?

        nodulatorFiles = _(files).filter (name) -> name.split('-').length > 1 and name.split('-')[0] is 'nodulator'
        process.stdout.write 'Ok. Found ' + nodulatorFiles.length + ' submodules.\n'

        generateMain nodulatorFiles

        i = 1
        async.eachSeries (_(nodulatorFiles).sortBy (item) -> _(moduleOrder).indexOf item), (file, done) ->
          binName = 'Nodulator-' + file.split('-')[1][0].toUpperCase() + file.split('-')[1][1..] + '.coffee'
          binPath = appRoot + '/node_modules/' + file + '/bin/' + binName
          exists binPath, (exist) ->
            return done() if not exist
          # fs.open binPath, 'r', (err, fd) ->
          #   return done() if err?

          #   fs.closeSync fd

            process.stdout.write '\n[' + i++ + ']Processing ' + binName.split('.')[0] + ': \n'
            (require binPath)(done)
        , done]

    , (err, results) ->
      return console.error err if err?

else if args[0] in ['install', 'remove']
  async.series [
    (done) ->
      if not args[1]?
        console.log 'Installing nodulator'
        return npm(args[0], 'nodulator', done)

      exists 'node_modules/nodulator', (exist) ->
        if exist
          console.log 'Already installed: nodulator'
          return done()

        console.log 'Installing nodulator'
        npm 'install', 'nodulator', done

    (done) ->
      #install dependencies
      if dependencies['nodulator-' + args[1]]? and args[0] is 'install'
        async.eachSeries dependencies['nodulator-' + args[1]], (item, done) ->
          exists 'node_modules/' + item, (exist) ->
            if not exist
              console.log 'Installing ' + item
              npm 'install', item, done
            else
              console.log 'Already installed: ' + item
              done()
        , done
      else
        done()

    (done) ->
      toInstall = 'nodulator-' + args[1]
      if toInstall in moduleOrder
        exists 'node_modules/' + toInstall, (exist) ->
          if exist
            console.log 'Already installed: ' + toInstall
            return done()

          console.log 'Installing ' + toInstall
          npm args[0], toInstall, done

      else
        console.error 'Unknown module: ', toInstall
        return done()]

  , (err) ->
    console.error err if err?

